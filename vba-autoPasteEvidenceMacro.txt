Option Explicit

'作業時はSub名をaa0001に変更すると楽
'aa0001_getPicSizeXY
'ax0001_getPicSizeXY
Sub ax0001_autoPasteEvidenceMacro()
'================================================================
'Copyright <2019> <notkitasima@gmail.com>

'Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:

'The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.

'THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
'================================================================
'
'【エビデンス画像ファイル貼り付けツール】
'
'【仕様】
'　C列に張り付けられます（調整可能）※ループ分画像の位置をスライド
'　エビデンス画像ファイルには見やすさのため枠線（黒）が付与されます（調整可能）※すべてのシェイプに枠線を追加する
'【使い方】
'　①エビデンス画像ファイルを張り付けたいシートを選択
'　②エビデンス画像ファイルを張り付けたい行を選択
'　③マクロを実行
'　④エビデンス画像ファイルを1つ以上選択
'　⑤エビデンス画像ファイル名は下記とする
'　　エビデンスファイル名フォーマット→試験番号_(n)_(画面／帳票／DB／他)_(n)_(OK／NG)
'　　例）試験番号_1_画面_1-1_OK.png
'　　例）試験番号_8_画面_2-4_OK.png
'　　※getFileNameAsEvidence()を変更することで対応可
'　⑥シートの構成
'　　B2に「No」
'　　C3にてウィンドウ枠の固定
'　　シート名自由
'
'===============================================================
    '変数宣言
    Dim pic As Object
    Dim sp As Shape
    Dim oWidth As Long
    Dim oHeight As Long
    Dim pWidth As Long
    Dim pHeight As Long
    Dim arrFile As Variant
    Dim arrFileItem As Variant
    Dim strFile As String
    Dim activeCellRow As Integer
    Dim activeCellCol As Integer
    Dim oneRowLenge As Double
    Dim oneRowLengeMultiplier As Integer
    Dim oneColumunLenge As Double
    Dim oneColumunLengeMultiplier As Integer
    Dim oneRowPointValue As Double
    Dim imageLeftValue As Long
    Dim imageTopValue As Long
    Dim iniFlag As Boolean
    Dim intBufOld As Integer
    Dim intBuf As Integer
    Dim max_oHeight As Long
    Dim pixelToPointValue As Integer
    
    '処理開始
    iniFlag = False
    
    '現在の選択セルアドレスを取得
    activeCellRow = ActiveCell.Row
    activeCellCol = ActiveCell.Column
    
    'エビデンス画像ファイル配置設定
    'セル横幅
    oneColumunLenge = 53.9
    'C列目から配置
    '1=>A列 2=>B列 3=>C列...
    oneColumunLengeMultiplier = 3
    'pixelの縦幅（セル）
    oneRowLenge = 14.3
    'pointの縦幅（セル）
    oneRowPointValue = 18.75
    '行目から配置
    '1=>1行 2=>2行 3=>3行...
    oneRowLengeMultiplier = 4
    
    'エビデンス画像ファイル初期配置（横）
    imageLeftValue = oneColumunLenge * oneColumunLengeMultiplier
    'エビデンス画像ファイル初期配置（縦）
    imageTopValue = oneRowLenge * oneRowLengeMultiplier
    
    'ダイアログ待機（複数選択可）
    arrFile = Application.GetOpenFilename(FileFilter:="全てのファイル,*.*", _
                                            Title:="画像ファイルを選択してください", _
                                            MultiSelect:=True) '複数選択オプション
    '初期フラグOn
    iniFlag = True
    
    'キャンセル処理
    If Not IsArray(arrFile) Then Exit Sub
    
    '画像処理 複数選択
    For Each arrFileItem In arrFile
        
        '変数入れ替え
        strFile = arrFileItem
        
        '初期化処理
        If iniFlag = True Then
            '初期化(一回目)
            iniFlag = False
            
            '項目番号取得
            intBuf = getFileNameAsEvidence(strFile)
            
            '高さ最大値
            max_oHeight = 0
            oHeight = 0
            
            '項目書番号記載
            Cells(2, 2) = "No"
            Cells(4, 2) = intBuf
            
        Else
            '画像高さ最大値をリセット
            max_oHeight = oHeight
            
            '次の画像配置場所（横）
            imageLeftValue = imageLeftValue + oWidth + oneColumunLenge
            
            '前回配置した画像の項目書番号取得
            intBufOld = intBuf
            
            '今回配置する画像の項目書番号取得
            intBuf = getFileNameAsEvidence(strFile)
            
            '項目書番号が異なる場合
            If intBuf <> intBufOld Then
                '配置する画像位置の初期化
                'エビデンス画像ファイル初期配置（横）
                imageLeftValue = oneColumunLenge * oneColumunLengeMultiplier
                
                '次の画像配置場所（縦）
                imageTopValue = imageTopValue + max_oHeight + (oneRowLenge * oneRowLengeMultiplier)
                
                'WIP数値丸め込み(計算式はよくわからない、、、\より/のほうが精度が良い）
                'WIP+2が一番制度が良い
                pixelToPointValue = imageTopValue / oneRowPointValue + 2
                '項目書番号記載
                Cells(pixelToPointValue, 2) = intBuf
                
                
            End If
            
        End If
        
        Select Case Mid(strFile, InStrRev(strFile, "."))
        
            Case ".bmp", ".ico", ".rle", ".wmf", ".emf", ".gif", ".jpg" ', ".JPG"
                Set pic = LoadPicture(strFile)
                pWidth = CLng(CDbl(pic.Width) * 24 / 635)
                pHeight = CLng(CDbl(pic.Height) * 24 / 635)
                
                'jpg貼り付け
                With ActiveSheet.Pictures.Insert(strFile)
                    .Top = Range("B3").Top
                    .Left = Range("B3").Left
                    ActiveSheet.Shapes(.Name).LockAspectRatio = msoFalse
                End With
                
            'png系とjpg系
            Case ".png", ".PNG", ".jpg", ".JPG", ".jpeg", ".JPEG" '※"JPG"だと反応する+問題なさそう
                Set sp = ActiveSheet.Shapes.AddPicture( _
                            Filename:=strFile, _
                            LinkToFile:=False, _
                            SaveWithDocument:=True, _
                            Left:=imageLeftValue, _
                            Top:=imageTopValue, _
                            Width:=0, _
                            Height:=0 _
                            )
                            'Top:=ActiveCell.Top, _
                            'Top:=imageTopValue, _

                With sp
                    .LockAspectRatio = msoTrue
                    .ScaleHeight 1, msoTrue
                    .ScaleWidth 1, msoTrue
                    oWidth = CLng(.Width)
                    oHeight = CLng(.Height)
                    pWidth = CLng(.Width * 4 / 3)
                    pHeight = CLng(.Height * 4 / 3)
                    '.Delete
                End With
                
                '高さ画像最大値更新
                If max_oHeight < oHeight Then max_oHeight = oHeight
                
                
                'Debug.Print "============================="
                'Debug.Print "imageTopValue:" & imageTopValue
                'Debug.Print "oWidth:" & oWidth
                
            Case Else 'その他の拡張子処理終了
                Exit Sub
                
        End Select
        'Debug.Print "横：" & pWidth & vbLf & "縦：" & pheight
        
    Next arrFileItem
    
    'すべてのシェイプに枠線を追加する
    Dim i As Integer
    For i = 1 To ActiveSheet.Shapes.Count
        With ActiveSheet.Shapes(i).Select
            With Selection.ShapeRange.Line
                .Visible = msoTrue
                .ForeColor.RGB = RGB(0, 0, 0)
                .Transparency = 0
            End With
        End With
    Next i
    
    '元のセルへ移動する
    Cells(activeCellRow, activeCellCol).Select
    
End Sub

Private Function getFileNameAsEvidence(ByVal strFileName As String) As Integer
'
'in str filename
'fnc regex
'out int evidence no
'
'ファイル名取得
strFileName = Mid(strFileName, InStrRev(strFileName, "\") + 1)

'"試験番号_"を置換(6文字目を表示)
strFileName = Mid(strFileName, 6)

'番号以外を置換("_"を置換)
strFileName = Left(strFileName, InStr(strFileName, "_") - 1)

'数値変換
'数値確認(入力値バリデーションチェック)
If IsNumeric(strFileName) Then
    '全角数字の場合半角数値へ変換
    strFileName = StrConv(strFileName, vbNarrow)
    
    '数値入力された場合StrからIntに変換
    'return
    getFileNameAsEvidence = Val(strFileName)
Else
    'return
    getFileNameAsEvidence = 0
End If

End Function